if (isRunGankModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'
android {
    compileSdkVersion 27



    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
                includeCompileClasspath true
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    resourcePrefix "gank_" //给 Module 内的资源名增加前缀, 避免资源名冲突


    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            if (isRunGankModule.toBoolean()) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    api project(':CommonSDK')
    api project(":CommonService")


    if (isRunGankModule.toBoolean()) {

        annotationProcessor "com.alibaba:arouter-compiler:1.1.4"
        annotationProcessor "com.jakewharton:butterknife-compiler:8.8.1"
        annotationProcessor 'com.google.dagger:dagger-compiler:2.15'

        debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
        releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'


    } else {
        compileOnly "com.alibaba:arouter-compiler:1.1.4"
        compileOnly "com.jakewharton:butterknife-compiler:8.8.1"
        compileOnly 'com.google.dagger:dagger-compiler:2.15'

        compileOnly 'com.squareup.leakcanary:leakcanary-android:1.5.4'
        compileOnly 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    }


}
